buildscript {
    ext {
        springBootVersion = '1.4.2.RELEASE'
        springloadedVersion = '1.2.4.RELEASE'
        dependencyManagementPluginVersion = '0.5.6.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: springBootVersion
        classpath group: 'org.springframework', name: 'springloaded', version: springloadedVersion
        classpath group: 'io.spring.gradle', name: 'dependency-management-plugin', version: dependencyManagementPluginVersion
    }
}

configure(allprojects) {
    ext {
        lombokVersion = '1.16.10'
        apacheCommsVersion = '3.6'
        h2Version = '1.4.190'
        mybatisBootVersion = '1.3.1'
        javamelodyVersion = '1.69.0'
        javamailapiVersion = '1.5.6'
        javamailVersion = '1.4.7'
        bootstrapVersion = '3.3.7'
    }

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: "io.spring.dependency-management"

    group = 'com.example'
    version = '0.0.1-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    dependencies {
        compile group: 'org.projectlombok', name: 'lombok', version: lombokVersion
        compile group: 'org.apache.commons', name: 'commons-lang3', version: apacheCommsVersion
        runtime group: 'com.h2database', name: 'h2', version: h2Version
    }

    configurations {
        all*.exclude group: 'commons-logging', module: 'commons-logging' // replaced with jcl-over-slf4j
        all*.exclude group: 'log4j', module: 'log4j' // replaced with log4j-over-slf4j
    }
}

project(':batch-server') {
    dependencies {
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-batch'
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-security'
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-velocity'
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf'
        compile group: 'org.springframework.boot', name: 'spring-boot-devtools'
//        providedCompile group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat'

        compile group: 'org.thymeleaf.extras', name: 'thymeleaf-extras-springsecurity4'
        compile group: 'org.webjars', name: 'bootstrap', version: bootstrapVersion

        compile group: 'org.mybatis.spring.boot', name: 'mybatis-spring-boot-starter', version: mybatisBootVersion
        compile group: 'net.bull.javamelody', name: 'javamelody-core', version: javamelodyVersion

        // java mail
        compile group: 'org.springframework', name: 'spring-context-support'
        compile group: 'javax.mail', name: 'javax.mail-api', version: javamailapiVersion
        compile group: 'javax.mail', name: 'mail', version: javamailVersion

        testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    }
}

project(':h2-server') {
    dependencies {
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
        compile group: 'com.h2database', name: 'h2', version: h2Version
    }
}

apply plugin: 'eclipse'

eclipse {
    classpath {
        containers.remove "org.eclipse.jdt.launching.JRE_CONTAINER"
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}
